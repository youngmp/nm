p eps=0,b=0
p omg=1,omt=1
p omfixt=0.9901981522494304
p omfixg=3.8590183230655155

##### Goodwin
p n=6,
p v1=.84,v2=.42,v4=.35,v6=.35,v8=1
p K=0.5,L=0,kc=1
p k1=1,k2=1,k3=.7,k4=1,k5=.7,k6=1,k7=.35,k8=1

F=0.5*(x+w)
x'=omfixg*omg*(v1*k1**n/(k1**n+z**n)-v2*x/(k2+x)+eps*K*F/(kc+K*F))
y'=omfixg*omg*(k3*x - v4*y/(k4+y))
z'=omfixg*omg*(k5*y - v6*z/(k6+z))
u'=omfixg*omg*(k7*x - v8*u/(k8+u))

##### thalamic model
p esyn=0
p el=-0.7,ena=0.5,ek=-.9,et=0
p gl=0.05,gna=3,gk=5,gt=5
p alpha=3,beta=.2
p ib=0.085

ah(v) = 0.128*exp(-5.556*(v+0.46))
bh(v) = 4/(1+exp(-20*(v+0.23)))

minf(v) = 1/(1+exp(-14.286*(v+0.37)))
hinf(v) = 1/(1+exp(25*(v+0.41)))
rinf(v) = 100/(1+exp(25*(v+0.84)))
pinf(v) = 0.01/(1+exp(-16.13*(v+0.6)))
ainf(v) = 1/(1+exp(-125*(v+0.2)))

tauh(v) = 1/(ah(v)+bh(v))  
taur(v) = 28+exp(-9.52*(v+0.25))  

iL(v) = gL*(v-eL)  
ina(v,h) = gna*minf(v)^3*h*(v-ena)  
ik(v,h) = gk*(0.75*(1-h))^4*(v-ek)  
it(v,r) = gt*pinf(v)^2*r*(v-et)
isyn(v,w) = w*(v-esyn)

v' = omfixt*(-iL(v)-ina(v,h)-ik(v,h)-it(v,r)+ib-eps*x*(v-esyn)-(b*eps+100*b^2*eps^2)/omfixt)*omt
h' = omfixt*((hinf(v)-h)/tauh(v))*omt
r' = omfixt*((rinf(v)-r)/taur(v))*omt
w' = omfixt*(alpha*(1-w)*ainf(v)-beta*w)*omt


x(0)=.046
y(0)=.5
z(0)=3
u(0)=.03

v(0)=-.05080318
h(0)=0.26303521
r(0)=0.2
w(0)=0.53071129



@ nplot=2
@ xp=t,yp=x
@ xp2=t,yp2=v
@ total=200,xlo=0,xhi=200,ylo=-1,yhi=1
@ done